Strict
Import mojo2
Import imagefilter

#GLFW_WINDOW_WIDTH=800
#GLFW_WINDOW_HEIGHT=600

Class myClass Extends App
	Field imgf:ImageFilter
	Field img:Image
	Field filter:Int,effect:Int
	Field cnvs:Canvas

	Method OnCreate:Int()
		SetUpdateRate(60)
		cnvs = New Canvas

		img = Image.Load("llandudno.jpg",0,0)

		imgf = New ImageFilter(img)
		imgf.CreateSnapshot()		
		
		filter=-1
		effect=1
				
		CreateImage()
			   
		Return 0
	End

	Method CreateImage:Void()
		imgf.RevertSnapshot()
		If filter>-1
			Select filter
			Case ImageFilter.SATURATE
				imgf.ApplyFilter(ImageFilter.SATURATE,Float(effect)/10.0)
			Case ImageFilter.BRIGHTNESS
				imgf.ApplyFilter(ImageFilter.BRIGHTNESS,Float(effect)/10.0)
			Case ImageFilter.GAUSSIAN
				imgf.ApplyFilter(ImageFilter.GAUSSIAN,effect*4)
			Case ImageFilter.MEAN
				imgf.ApplyFilter(ImageFilter.MEAN,effect*4)
			Case ImageFilter.REDUCE
				imgf.ApplyFilter(ImageFilter.REDUCE,Pow(2,effect+1))
			Case ImageFilter.TINT
				imgf.ApplyFilter(ImageFilter.TINT,[$ff00ff,1/4.0*effect])
			Default
				imgf.ApplyFilter(filter,effect)
			End
		End
	End
	
	Method OnUpdate:Int()
		If KeyHit(KEY_1)
			effect=1
			CreateImage()
		End
		If KeyHit(KEY_2)
			effect=2
			CreateImage()
		End
		If KeyHit(KEY_3)
			effect=3
			CreateImage()
		End

		If KeyHit(KEY_SPACE)
			filter+=1
			If filter>ImageFilter.BRIGHTNESS Then filter=-1
			CreateImage()
		End
		Return 0
	End

	Method OnRender:Int()
		cnvs.Clear (0,0.5,0)
		cnvs.DrawImage imgf.GetImage(),0,0
		
		cnvs.DrawText "Press SPACE to change filter",20,20
		cnvs.DrawText "Press 1-3 to change effect of filter",20,40
		
		cnvs.Flush

		Return 0
	End
End

Function Main:Int()
	' Create an instance of your class that you have defined above.
	New myClass
	Return 0
End